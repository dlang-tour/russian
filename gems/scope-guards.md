# Стражи области видимости

Стражи области видимости позволяют исполнять выражения
во время выхода из текущего блока, при определённых условиях:

* `scope(exit)` всегда вызывается
* `scope(success)` вызывается при отсутствии исключений
* `scope(failure)` вызывается, если до выхода из блока
  выбрасывается исключение

Использование стражей области видимости делает код более явным,
и позволяет поместить занятие и освобождение ресурсов
непосредственно рядом друг с другом.
Эти маленькие помощники так же усиливают безопасность,
позволяя удостовериться, что определённый код *всегда*
будет выполнен, вне зависимости от ветки исполнения.

В D `scope` фактически заменяет идиому RAII, применяемую
в C++ и часто используемую при реализации специальных
стражей для определённых типов ресурсов.

Стражи области видимости вызываются в порядке, обратном
их определению.

### Подробнее

- [`scope` in _Programming in D_](http://ddili.org/ders/d.en/scope.html)

## {SourceCode}

```d
import std.stdio : writefln, writeln;

void main()
{
    writeln("<html>");
    scope(exit) writeln("</html>");

    {
        writeln("\t<head>");
        scope(exit) writeln("\t</head>");
        "\t<title>%s</title>".writefln("Hello");
    } // scope(exit) из предыдущей строки
      // будет исполнен здесь

    writeln("\t<body>");
    scope(exit) writeln("\t</body>");

    writeln("\t\t<h1>Hello World!</h1>");

    // страж позволяет поместить выделение
    // и освобождение памяти непосредственно
    // рядом друг с другом
    import core.stdc.stdlib : free, malloc;
    int* p = cast(int*) malloc(int.sizeof);
    scope(exit) free(p);
}
```
